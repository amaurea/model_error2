set xlabel "Wavenumber (ℓ)" offset 0,0.5
set ylabel "Fraction of power retained" offset 1.0,0
set key above
set xrange [0:0.5]
set yrange [0:1.05]
set term svg size 600,400 font ",17"
set output "gain_toy2d_bias.svg"
plot \
	"gain_toy2d_binned_bias_ps.txt" u 1:2 w l lw 2 lc 1 title "bin", \
	"gain_toy2d_ml_bias_ps.txt"     u 1:($2*1.026) w l lw 2 lc 3 title "ML", \
	"gain_toy2d_ml_cap_1_bias_ps.txt" u 1:($2*1.026) w l lw 0.3 lc 3 title "", \
	"gain_toy2d_ml_cap_2_bias_ps.txt" u 1:($2*1.026) w l lw 0.3 lc 3 title "", \
	"gain_toy2d_ml_cap_3_bias_ps.txt" u 1:($2*1.026) w l lw 0.3 lc 3 title "", \
	"gain_toy2d_fbin_bias_ps.txt"   u 1:($2*1.048) w l lw 2 lc 4 title "FB"

set key width -3 samplen 2
set yrange [0.9:100]
set ylabel "Debiased relative noise power" offset 1.0,0
set logscale y
set output "gain_toy2d_noise.svg"
plot \
	"<paste gain_toy2d_binned_noise_debiased_ps.txt gain_toy2d_ml_noise_debiased_ps.txt | awk '$1>0'" u 1:($2/$4/1e4) w l lw 2 title "bin/1e4", \
	for [i=1:3] sprintf("<paste gain_toy2d_ml_cap_%d_noise_debiased_ps.txt gain_toy2d_ml_noise_debiased_ps.txt | awk '$1>0'",i) u 1:($2/$4) w l lc 3 lw 0.5 title i==1?"ML w1,w2,w3":"", \
	"<paste gain_toy2d_fbin_noise_debiased_ps.txt gain_toy2d_ml_noise_debiased_ps.txt | awk '$1>0'" u 1:($2/$4) w l lw 2 lc 4 title "FB"


# 1d toy example
unset logscale y
set yrange [0:1]
set xrange [0:1000]
set key samplen 3
set xlabel "Multipole (ℓ)"
set ylabel "Fraction of power retained" offset 1.0,0
set output "gain_toy1d_bias.svg"
set term svg size 500,350 font ",15"
plot "gain_toy1d_bias.txt" u 1:2 w l lw 2 title "theory", "gain_1d_toy_sim.txt" u 1:2 w l lw 2 dt 2 title "sim"
