set key above
set xlabel "Pixel (x)" offset 0, 0.5
set ytics 0.25
set xtics 1

set dt 2 (28,12)
set dt 3 (20,20)
set dt 4 (12,28)
set pointsize 0.7

set output "model_error_toy.svg"
set term svg size 600,400 font ",14"
plot "signal.txt" u 1:2 w l lw 2 title "signal", "maps.txt" u 1:2 w lp lw 2 title "binned", "" u 1:3 w lp lw 2 title "ML", "" u 1:4 w lp lw 2 dt 2 title "FB deobs", "" u 1:5 w lp lw 2 title "FB detrans", "" u 1:6 w lp lw 2 dt 4 title "destripe"

set output "model_error_toy_noerr.svg"
plot "signal.txt" u 1:3 w l lw 2 title "signal", "maps.txt" u 1:7 w lp lw 2 dt 2 title "binned", "" u 1:8 w lp lw 2 dt 3 title "ML", "" u 1:9 w lp lw 2 dt 4 title "FB deobs", "" u 1:10 w lp lw 2 title "FB detrans", "" u 1:11 w lp lw 1 title "destripe"


set xlabel "Frequency"
set ylabel "Power" offset 0.7,0
set logscale xy
set xtics 2
unset key
unset mxtics
set term svg size 600,400 font ",16"
set output "ps.svg"
plot "ps.txt" u 1:2 w l lw 2

# Plot the residuals too
set xlabel "Pixel (x)"
set dt 2 (16,4)
set xtics 1
unset logscale xy
set yrange [-1:1]
set key samplen 3 width -2.5 font ",14"
set term svg size 700,400 font ",16" lw 0.7
set output "model_resids.svg"
plot \
	"resids.txt" u 1:2 w l lc 1 lw 5 title "signal", \
	"resids.txt" u 1:3 w l lc 3 lw 3 title "binned", \
	"resids.txt" u 1:4 w l lc 4 lw 3 title "zero", \
	"resids.txt" u 1:5 w l lc 5 lw 3 title "ML", \
	"resids.txt" u 1:6 w l lc 3 lw 3 dt 2 title "", \
	"resids.txt" u 1:7 w l lc 4 lw 3 dt 2 title "", \
	"resids.txt" u 1:8 w l lc 5 lw 3 dt 2 title "", \
	-2 w l lc -1 lw 3 dt 1 title "model", \
	-2 w l lc -1 lw 3 dt 2 title "resid"

set dt 2 (8,4)
set format y "10^{%T}"
set xlabel "Frequency" offset 0,0.5
set xtics ()
set for [i=0:10] xtics add (sprintf("%.1f", i*0.1) (i*0.1)**0.5)
set xtics add ("0.01" 0.01**0.5) ("0.05" 0.05**0.5)
set ytics auto
set logscale y
set ylabel "Chisquare above freq" offset 2,0
set yrange [0.1:*]
set key samplen 2 width -2.5 font ",14"
set term svg size 800,400 font ",16"
set output "model_resid_cumps.svg"
plot \
	"chisqs.txt" u ($1**0.5):11 w l lc 3 lw 2 title "binned", \
	"chisqs.txt" u ($1**0.5):12 w l lc 4 lw 2 title "zero", \
	"chisqs.txt" u ($1**0.5):13 w l lc 5 lw 2 title "ML", \
	"chisqs.txt" u ($1**0.5):8  w l lc 3 lw 2 dt 2 title "", \
	"chisqs.txt" u ($1**0.5):9  w l lc 4 lw 2 dt 2 title "", \
	"chisqs.txt" u ($1**0.5):10 w l lc 5 lw 2 dt 2 title "", \
	-2 w l lc -1 lw 3 dt 1 title "weighted", \
	-2 w l lc -1 lw 3 dt 2 title "plain"
