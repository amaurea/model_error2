set xlabel "Wavenumber (l)"   offset 0,0.6
set ylabel "Fraction of power retained" offset 0.8,0
set xrange [0:0.5]
set yrange [0.99:1.01]
set key above width -4 samplen 1 font ",12"
set dt 2 (5,3)
set dt 3 (3,2)
cut0(x,y) = x == 0 ? 0.0/0 : y
sinc(x) = abs(x) < 1e-6 ? 1 : sin(pi*x)/(pi*x)

# Signal
set term svg size 600,400 lw 2 font ",16"
set output "model_error_toy_2d_noerr.svg"
plot \
	"<paste toy2d_theory_ps.txt toy2d_binned_nn_signal_ps.txt"   u 1:(cut0($1,$4/$2)) w l lw 4 lc 1 dt 1 title "bin", \
	"<paste toy2d_theory_ps.txt toy2d_ml_nn_signal_ps.txt"       u 1:(cut0($1,$4/$2)) w l lw 2 lc rgb "#0000ff" title "ML", \
	"<paste toy2d_theory_ps.txt toy2d_ml_cap_1_nn_signal_ps.txt" u 1:(cut0($1,$4/$2)) w l lc rgb "#8888ff" title "ML w1", \
	"<paste toy2d_theory_ps.txt toy2d_ml_cap_2_nn_signal_ps.txt" u 1:(cut0($1,$4/$2)) w l lc rgb "#aaaaff" title "ML w2", \
	"<paste toy2d_theory_ps.txt toy2d_ml_cap_3_nn_signal_ps.txt" u 1:(cut0($1,$4/$2)) w l lc rgb "#ddddff" title "ML w3", \
	"<paste toy2d_theory_ps.txt toy2d_ml_lin_signal_ps.txt"      u 1:(cut0($1,$4/$2)) w l lw 3 lc rgb "#aa00ff" title "ML lin" , \
	"<paste toy2d_theory_ps.txt toy2d_destripe_plain_004_nn_signal_ps.txt" u 1:(cut0($1,$4/$2)) w l lc rgb "#ffaa00" title "DS 4" , \
	"<paste toy2d_theory_ps.txt toy2d_destripe_prior_004_nn_signal_ps.txt" u 1:(cut0($1,$4/$2)) w l dt 2 lc rgb "#ffaa00" title "DS+ 4" , \
	"<paste toy2d_theory_ps.txt toy2d_destripe_plain_016_nn_signal_ps.txt" u 1:(cut0($1,$4/$2)) w l lc rgb "#ffcc88" title "DS 16" , \
	"<paste toy2d_theory_ps.txt toy2d_destripe_prior_016_nn_signal_ps.txt" u 1:(cut0($1,$4/$2)) w l dt 2 lc rgb "#ffcc88" title "DS+ 16" , \
	"<paste toy2d_theory_ps.txt toy2d_destripe_plain_064_nn_signal_ps.txt" u 1:(cut0($1,$4/$2)) w l lc rgb "#ffddaa" title "DS 64" , \
	"<paste toy2d_theory_ps.txt toy2d_destripe_prior_064_nn_signal_ps.txt" u 1:(cut0($1,$4/$2)) w l dt 2 lc rgb "#ffddaa" title "DS+ 64" , \
	"<paste toy2d_theory_ps.txt toy2d_destripe_prior_004_lin_signal_ps.txt" u 1:(cut0($1,$4/$2)) w l lc rgb "#00ff00" title "DS+ 4 lin" , \
	"<paste toy2d_theory_ps.txt toy2d_binned_nn_signal_ps.txt toy2d_fbinsim_nn_signal_ps.txt toy2d_fbin_nn_signal_ps.txt" u 1:(cut0($1,$8/($6/$4)/$2)) w l lc rgb "#ff0000" dt 2 lw 2 title "FB" , \
	"<paste toy2d_theory_ps.txt toy2d_binned_lin_signal_ps.txt toy2d_fbinsim_lin_signal_ps.txt toy2d_fbin_lin_signal_ps.txt" u 1:(cut0($1,$8/($6/$4)/$2)) w l lc rgb "#ff0000" dt 3 title "FB lin" , \
	-1 title ""
